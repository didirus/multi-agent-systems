to lead-auction
  if in-auction? [handle-auction stop]
  if initiate-auction? [ initiate-auction stop]
end

to-report initiate-auction?
  
  ; Based on random dice throw, declare myself to be auctioned.
  
  let dice_threshold 0.002
  let dice_throw random-float 1
  report dice_throw <= dice_threshold
end

to initiate-auction
  mark
  ; I decided to give myself up for auctioning. I will now broadcast this.
  
  broadcast "auction" auction.initiating_auction capacity?
  set auction/state auction:AWAITING_RESPONSE
end

to-report in-auction? report auction/state != auction:UNINITIATED end

to handle-auction
  
  if auction/state = auction:AWAITING_RESPONSE [
    
    ; Just opened the action. Waiting for bids.
    set auction/state auction:ABOUT_TO_AUCTION
    stop]
  
  if auction/state = auction:ABOUT_TO_AUCTION [ 
    
    ; Bids are in. Picking the winner.   
    finish-auction
    stop]
end

to finish-auction
  ifelse auction/alone_in_district [
    if debug_auctions [ debug (word "I was about to auction myself, but quit since I found out I'm the only one in my district")]]
  [
    ; Reassign myself to the winning district.
    get-assigned-to auction/winning_district]
  
  clear-auction-memory
end

to clear-auction-memory
  set auction/state auction:UNINITIATED
  set auction/winner false
  set auction/winning_district false
  set auction/winning_bid -99
  set auction/alone_in_district true
end

to follow-auction [sender body details]
  
  if body = auction.initiating_auction and not in-auction? and assigned-to-district? [
    
    let offer fullness? ; The less capacity I have left, the more I need to win the auction.
    send sender "auction" auction.this_is_my_bid (list offer district)
    stop]
  
  if in-auction? and body = auction.this_is_my_bid [
    
    ; If the message is from my own district, I know I'm not alone.
    if sender-district-of details = district [ set auction/alone_in_district false]
    
    ; See if this offer is the best offer so far.
    if offer-of details > auction/winning_bid [
      set auction/winner sender
      set auction/winning_district sender-district-of details
      set auction/winning_bid offer-of details]
    stop]
end

to-report offer-of [details] report first details end
to-report sender-district-of [details] report item 1 details end