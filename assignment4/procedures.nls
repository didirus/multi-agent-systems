; This file contains all kinds of auxiliary procedures.

; Returns list with the IDs of all buses, except my own.
to-report all-buses? report socialnetwork/collegues  end


to-report newest-bus? ; Returns the ID of the newest bus.
  report max fput bus_id all-buses?
end

to-report oldest-bus? ; Returns the ID of the oldest bus.
  report min fput bus_id all-buses?
end
 
to-report waiting-passengers? ; Returns whether there are waiting passengers.
  report any? bus_stops with [length passengers_waiting > 0]
end

to-report overloaded? ; Returns whether this bus is full.
  report capacity? = length bus_passengers
end

to-report capacity? ; Returns my capacity according to my bus type.
  if bus_type = 1 [report bus_type1_capacity]
  if bus_type = 2 [report bus_type2_capacity]
  if bus_type = 3 [report bus_type3_capacity]
end

to-report at-junction? ; Returns whether I'm currently at a junction.
  let boolean false
  
  ; for each district list/set
  foreach districts [
    let pos position ? districts
    
    ; for all other districts
    if pos != district [
      
      ; if I'm at any junction, return true
      if member? current_stop table:get junctions (list district pos) [
        set boolean true ]]]
  
  ; return outcome
  report boolean
end

to-report in-transit?
  report not (next_stop = -1 or next_stop = false)
end

to-report assigned-to-district? ; Returns whether I'm currently assigned to a district.
  report district != false
end

to get-assigned-to [ new_district ]
  if debug_messages [ debug (word "I have been assigned to D" new_district ".")]
  set district new_district
  reset-overloaded/other_district_blockers
end

to-report in-my-district [destination] ; Returns whether `destination` is stop in my district.
  report member? destination item district districts
end

to-report intersect [a b] ; Returns intersection of lists `a` and `b`.
  report (filter [ member? ? b ] a)
end

to-report any [collection predicate] ; Returns whether `predicate` is true for each element of `collection`.
  report not empty? collection and reduce [?1 or (runresult predicate ?2)] (fput false collection)
end

to-report successor-of [element collection] ; Returns the successor of `element` in `collection`.
  assert (word "`successor-of` called, but `" element "` is not an element of `" collection "`.") member? element collection
  report item ((position element collection + 1) mod length collection) collection
end
  
to-report index-in-range [ number ]
  let range []
  let i 0
  while [i < number] [
     set range lput i range
     set i i + 1]
  report range
end

to-report enumerate [ collection ]
  report index-in-range length collection
end

to-report home?
  report member? location? stops-of district
end

to-report just-came-home?
  report previous_stop = -1
   or not member? previous_stop stops-of district
   or not (successor-of previous_stop stops-of district = location?)
end

to-report stops-of [ some-district ]
  report item some-district districts
end

to-report location?
  if in-transit? [ warn "Procedure `location?` used while in transit (not at bus stop)." ]
  report current_stop
end

to-report determine-junctions? ; Returns table mapping pair of districts to their overlapping stops
  let junction_table table:make
  foreach enumerate districts [ let district_a ?
    foreach enumerate districts [ let district_b ?
      if district_a != district_b [
        table:put junction_table (list district_a district_b)
         reduce intersect (list stops-of district_a stops-of district_b) ]]]
  
  report junction_table
end

to debug [text]
  let preamble ""
  
  if debug_show_ticks [ set preamble (word preamble "[" ticks "]")]
  
  if debug_show_districts [
    ifelse assigned-to-district? [ 
      set preamble (word preamble "[D" district "]")]
    [
      set preamble (word preamble "[--]")]]
  
  show (word preamble " " text)
end

to warn [text]
  mark
  debug (word "WARNING! " text)
  mark
end

to assert [ text predicate ] if not predicate [ warn text ] end
  
to reset-overloaded/other_district_blockers
  set overloaded/other_district_blockers table:make
  foreach districts [
    let i position ? districts
    if i != district [
      table:put overloaded/other_district_blockers i []
    ]
  ] 
end

to handle-timers
  
  ; handle buy timer
  if overloaded/timer > 0 [
    ; Decrease waiting time to buy new buses.
    set overloaded/timer overloaded/timer - 1]
  
  ; handle reallocation timer
  if overloaded/reallocation_timer > 0 [
    set overloaded/reallocation_timer (overloaded/reallocation_timer - 1)]
end

to mark
  print marker
end

to mark-personal
  debug marker
end